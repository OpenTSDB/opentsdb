
apply plugin: 'application'

mainClassName = "net.opentsdb.Main"
applicationName = "tsdb"
applicationDefaultJvmArgs = [ "-server", "-XX:+UseG1GC", "-XX:+AggressiveOpts", "-XX:+UseFastAccessorMethods" ]

dependencies {
    compile project(":tsdb-core")
    compile project(":tsdb-cassandra")

    compile project(":idmanager:tsdb-assign")
    compile project(":tsdb-web")
}

run {
    systemProperties = [
        "app.home": rootDir
    ]
}

startScripts.with {
    doLast {
        unixScript.text = unixScript.text.replaceAll('cd "\\$SAVED" \\>\\&-\n', '$0\nDEFAULT_JVM_OPTS="-Dapp.home=\\$APP_HOME"\n')
        windowsScript.text = windowsScript.text.replaceAll('\r\n@rem Find java.exe', '\r\nset DEFAULT_JVM_OPTS=-Dapp.home=%APP_HOME%\r\n$0')
    }
}

distributions {
    main {
        baseName = rootProject.name

        contents {
            into('') {
                from { project(':tsdb-core').distFiles }
                from { project(':tsdb-cassandra').distFiles }
            }
        }
    }
}
