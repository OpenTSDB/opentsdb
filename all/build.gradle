
apply plugin: 'application'

mainClassName = "net.opentsdb.TSDB"
applicationName = "tsdb"

dependencies {
    compile project(":tsdb-core")
    compile project(":tsdb-cassandra")
    compile project(":tsdb-api")
    compile project(":tsdb-tools")
    compile project(":tsdb-storage-json")

    compile project(":idmanager:tsdb-assign")
}

run {
    systemProperties = [
        "app.home": rootDir
    ]
}

startScripts.with {
    doLast {
        unixScript.text = unixScript.text.replaceAll('cd "\\$SAVED" \\>\\&-\n', '$0\nDEFAULT_JVM_OPTS="-Dapp.home=\\$APP_HOME"\n')
        windowsScript.text = windowsScript.text.replaceAll('\r\n@rem Find java.exe', '\r\nset DEFAULT_JVM_OPTS=-Dapp.home=%APP_HOME%\r\n$0')
    }
}

distributions {
    main {
        baseName = rootProject.name

        contents {
            exclude ".gitkeep"

            // Add the start scripts for all sibling projects that are
            // applications.
            into('bin') {
                from { project(':tsdb-api').startScripts.outputs.files }
                fileMode = 0755
            }

            into('') {
                from { project(':tsdb-core').distFiles }
                from { project(':tsdb-cassandra').distFiles }
            }
        }
    }
}
