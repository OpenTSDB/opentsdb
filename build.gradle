
subprojects {
    apply plugin: 'java'
    apply plugin: 'checkstyle'

    sourceCompatibility = JavaVersion.VERSION_1_6

    group = 'net.opentsdb'
    version = "2.1.0+${getGitAbbreviatedHash()}"

    checkstyle {
        configFile = new File(rootDir, "checkstyle.xml")
        toolVersion = 6.3
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint"
    }

    test {
        maxHeapSize = "2048m"
        jvmArgs '-XX:MaxPermSize=256m'

        forkEvery = 1
        maxParallelForks = 2
    }

    jar {
        manifest {
            attributes(
                    "Implementation-Title": "OpenTSDB",
                    "Implementation-Vendor": "The OpenTSDB Authors",
                    "Implementation-Version": version,
                    "Build-User": System.getenv("USER"),
                    "Build-Host": InetAddress.getLocalHost().getHostName(),
                    "Build-Date": new Date())
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        compile group: 'com.typesafe', name: 'config', version: '1.2.1'
        compile group: 'com.squareup.dagger', name: 'dagger', version: '1.2.2'
        compile group: 'com.google.guava', name: 'guava', version: '18.0'
        compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.4.3'
        compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.4.3'
        compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.4.3'
        compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-guava', version: '2.4.3'
        compile group: 'com.stumbleupon', name: 'async', version: '1.4.0'
        compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.7'
        compile group: 'org.hbase', name: 'asynchbase', version: '1.6.0'

        compile(group: 'org.apache.zookeeper', name: 'zookeeper', version: '3.3.6') {
            exclude group: 'log4j', module: 'log4j'
            exclude group: 'org.slf4j', module: 'slf4j-log4j12'
            exclude group: 'jline', module: 'jline'
            exclude group: 'junit', module: 'junit'
        }

        // Compile only
        compile group: 'com.google.auto.service', name: 'auto-service', version: '1.0-rc2'
        compile group: 'com.squareup.dagger', name: 'dagger-compiler', version: '1.2.2'

        // Runtime only?
        compile group: 'org.slf4j', name: 'log4j-over-slf4j', version: '1.7.7'
        compile group: 'ch.qos.logback', name: 'logback-core', version: '1.0.13'
        compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.0.13'
        compile group: 'io.dropwizard.metrics', name: 'metrics-core', version: '3.1.0'
        compile group: 'io.dropwizard.metrics', name: 'metrics-jvm', version: '3.1.0'

        testCompile group: 'org.hamcrest', name: 'hamcrest-core', version: '1.3'
        testCompile group: 'org.javassist', name: 'javassist', version: '3.18.1-GA'
        testCompile group: 'junit', name: 'junit', version: '4.11'
        testCompile group: 'org.mockito', name: 'mockito-core', version: '1.9.5'
        testCompile group: 'org.objenesis', name: 'objenesis', version: '1.3'
        testCompile group: 'org.powermock', name: 'powermock-api-mockito', version: '1.5.4'
        testCompile group: 'org.powermock', name: 'powermock-module-junit4', version: '1.5.4'
    }
}

def getGitAbbreviatedHash() {
    return "git rev-parse --verify --quiet --short HEAD".execute().in.text.trim()
}