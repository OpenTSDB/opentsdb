
task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: "signing"

    sourceCompatibility = JavaVersion.VERSION_1_7
    targetCompatibility = JavaVersion.VERSION_1_7

    group = 'net.opentsdb'
    version = "2.1.0+${getGitAbbreviatedHash()}"

    repositories {
        mavenCentral()
    }

    configurations {
        provided
    }

    ext {
        libraries = [
                config: 'com.typesafe:config:1.2.1',
                guava: 'com.google.guava:guava:18.0',
                slf4j: 'org.slf4j:slf4j-api:1.7.7',
                logback: 'ch.qos.logback:logback-classic:1.0.13',
                auto_value: 'com.google.auto.value:auto-value:1.0',
                auto_service: 'com.google.auto.service:auto-service:1.0-rc2',
                jopt: 'net.sf.jopt-simple:jopt-simple:4.9-beta-1',

                dagger: 'com.squareup.dagger:dagger:1.2.2',
                dagger_compiler: 'com.squareup.dagger:dagger-compiler:1.2.2',

                metrics_core: 'io.dropwizard.metrics:metrics-core:3.1.0',
                metrics_jvm: 'io.dropwizard.metrics:metrics-jvm:3.1.0',
                metrics_json: 'io.dropwizard.metrics:metrics-json:3.1.0',

                cassandra: 'com.datastax.cassandra:cassandra-driver-core:2.1.3',

                // Web (Netty)
                netty: 'io.netty:netty-all:4.1.0.Beta4',
                javassist: 'org.javassist:javassist:3.19.0-GA',

                jackson_core: 'com.fasterxml.jackson.core:jackson-core:2.4.3',
                jackson_databind: 'com.fasterxml.jackson.core:jackson-databind:2.4.3',
                jackson_annotation: 'com.fasterxml.jackson.core:jackson-annotations:2.4.3',
                jackson_guava: 'com.fasterxml.jackson.datatype:jackson-datatype-guava:2.4.3',

                // Tests
                junit: 'junit:junit:4.11',
                hamcrest: 'org.hamcrest:hamcrest-core:1.3',
                mockito: 'org.mockito:mockito-core:1.9.5',

                // Legacy
                async: 'com.stumbleupon:async:1.4.0',
                objenesis: 'org.objenesis:objenesis:1.3',
                powermock_api_mockito: 'org.powermock:powermock-api-mockito:1.5.4',
                powermock_module_junit: 'org.powermock:powermock-module-junit4:1.5.4'
        ]
    }

    dependencies {
        compile libraries.guava,
                libraries.slf4j,
                libraries.async

        // Compile only
        provided libraries.dagger_compiler

        testCompile libraries.junit,
                    libraries.hamcrest,
                    libraries.mockito
    }

    sourceSets {
        main {
            compileClasspath += configurations.provided
        }
        test {
            compileClasspath += configurations.provided
        }
    }

    compileJava {
        options.compilerArgs << "-Xlint"
        options.encoding = "UTF-8"
    }

    test {
        maxHeapSize = "2048m"
        jvmArgs '-XX:MaxPermSize=256m'

        forkEvery = 1000
        maxParallelForks = 1
    }

    jar {
        manifest {
            attributes(
                    "Implementation-Title": "OpenTSDB",
                    "Implementation-Vendor": "The OpenTSDB Authors",
                    "Implementation-Version": version,
                    "Build-User": System.getenv("USER"),
                    "Build-Host": InetAddress.getLocalHost().getHostName(),
                    "Build-Date": new Date())
        }
    }

    signing {
        required false
        sign configurations.archives
    }

    checkstyle {
        configFile = new File(rootDir, "checkstyle.xml")
        toolVersion = 6.3
    }
}

def getGitAbbreviatedHash() {
    return "git rev-parse --verify --quiet --short HEAD".execute().in.text.trim()
}